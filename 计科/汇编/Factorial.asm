;本代码可以计算50以内的阶乘
.386
DATA 	SEGMENT	USE16
BUF	DB	4
		DB	?
		DB	4	DUP(0)
RET1	DD	120	DUP(0)
CLR	DB	0AH,0DH,'$'
DATA	ENDS

STACK	SEGMENT	USE16	STACK
		DB	200	DUP(0)
STACK	ENDS
CODE	SEGMENT	USE16
	ASSUME	CS:CODE,SS:STACK,DS:DATA
START:

WTONUM MACRO B      ;将字符转化为数字
	LOCAL	P
	CMP	B,'A'
	JB		P
	SUB	B,'A'-'9'-1
P:
	SUB	B,'0'
	ENDM	
	
	MOV	AX,DATA
	MOV	DS,AX
	LEA	DX,BUF
	MOV	AH,10
	INT	21H                    ;输入数字
	
	XOR	EAX,EAX
	XOR	ECX,ECX
	MOV	SI ,OFFSET BUF+2
P:
	MOV	CL,[SI]
	CMP	CL,0DH
	JE		Q
	WTONUM	CL
	IMUL	EAX,10
	ADD	EAX,ECX
	INC	SI
	JMP	P
Q:
	XOR	EDX,EDX 
	XOR	ECX,ECX         ;进位
	XOR	EBX,EBX		;计数
	MOV	EBX,EAX 
	MOV	RET1,EAX
	DEC	EBX
Q1:	MOV	SI,OFFSET	RET1
	
Q2:	
	MOV	EAX,[SI]
	MUL	EBX
	ADD	EAX,ECX
	JNC	NC
	ADD	EDX,1
NC:
	MOV	ECX,EDX        ;将进位从EDX->ECX
	XOR	EDX,EDX
	MOV	[SI],EAX
	ADD	SI,4
	CMP	SI,OFFSET RET1+4*119   ;一次乘法是否完成
	JB		Q2
	DEC	EBX	
	CMP	EBX,0	
	JNE		Q1	
;输出结果
	MOV	ECX,10			;基数10
	XOR	SI,SI			;输出计数
S1:
	MOV	DI,OFFSET	RET1+4*119
	XOR	EDX,EDX
	MOV	BL,0
S3:
	SUB	DI,4
	MOV	EAX,[DI]
	DIV	ECX
	MOV	[DI],EAX
	CMP	EAX,0
	JE		S2
	MOV	BL,1		;FLAG用来判断是否为0
S2:	
	CMP	DI,OFFSET RET1
	JA		S3
	ADD	DL,'0'
	PUSH	DX
	INC	SI
	CMP	BL,1
	JE		S1
	
	MOV	AH,2
Show:
	POP	DX
	INT	21H
	DEC	SI
	CMP	SI,0
	JNE	Show
	
	MOV	AH,4CH
	INT	21H
CODE	ENDS
	END	START


